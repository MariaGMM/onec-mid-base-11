#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьФорму(Форма) Экспорт
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
		ДобавитьПолеСогласованнаяСкидкаВГруппаПересчитать(Форма);    
		ДобавитьКнопкуПересчитатьВГруппаПересчитать(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_КонтактноеЛицо";
КонецПроцедуры     

Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_КонтактноеЛицо";
КонецПроцедуры 

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаПересчитать(Форма)
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаПересчитать);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Дораб_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Дораб_ПересчитатьПриИзмененииСогласованнаяСкидка");
КонецПроцедуры

Процедура ДобавитьКнопкуПересчитатьВГруппаПересчитать(Форма)   
	Команда = Форма.Команды.Добавить("Пересчитать");
	Команда.Заголовок = "Пересчитать";
	Команда.Действие = "Дораб_Пересчитать";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаПересчитать);
	КнопкаФормы.ИмяКоманды = "Пересчитать";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры


#КонецОбласти 

